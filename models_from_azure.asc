# Script that imports information about Devices, Device templates and Dashboards from Azure:
#	it creates a new model within the information got by API calls.

# API calls format is inside the "azure.json" file


#COMMENT OUT THE FOLLOWING LINE TO CALL THE SCRIPT FROM EXT COUPLING AS PROCEDURE INSTEAD OF DOING "EXECUTE file:(<local_path>\\models_from_azure.asc)"
MODELS_FROM_AZURE

# TODO: prendere la stringa per le chiamate API da "azure.json" (guarda thingsboard.json ---> path, type, return) 
PROCEDURE global MODELS_FROM_AZURE
{    
	# todo accomedare per bene, prendere esempio dagli script di davide, prima fare quelli appena torna da milano
	# CC "Explorer" GET_SELECTED_MODELGROUPS

	CC "CoreUI" MODEL_SELECT_BOX without-models mgroup-sel title:"Select a target modelgroup for new IoT Scenario - PIM model"
    IF (endbutton != "ok") {
        EXIT
    }
	CC "AdoScript" EDITFIELD title:("Insert new model name...") caption:("~Modelname:")
    IF (ecode != 0) {
    	EXIT
    }
    CC "Core" CREATE_MODEL modeltype:("IoT Scenario") modelname:(text) version:"" mgroups:(mgroupids)
	SET a_dstmodelid:(modelid)

	# PRESET_PSM platform:"Azure"
	CC "Modeling" OPEN modelids:(STR a_dstmodelid)
	CC "Modeling" SET_VIEW_MODE modelid:(a_dstmodelid) mode-name:"PSM"
    CC "Core" SET_ATTR_VAL objid:(a_dstmodelid) attrname:"Model Type" val:("PSM")
    CC "Core" SET_ATTR_VAL objid:(a_dstmodelid) attrname:"Platform" val:("Azure")

	# If the original model still remains as "PIM", it means the PRESET_PSM procedure didn't success
	#CC "Core" GET_ATTR_VAL objid:(a_dstmodelid) attrname:("Model Type")
	#CC "AdoScript" INFOBOX("a dst modelid " + STR a_dstmodelid)
	#IF(val="PIM") {
	#	CC "Core" DELETE_MODEL modelid:(a_dstmodelid)
	#	EXIT
	#}

	# TODO forse si puo indicare tutto come Subdomain anziche name, name puo essere confuso, ci possono essere dei fraintendimenti


	# Get application name TODO accomedare
	SET applicationURL: ("apptogepi")
	#CC "AdoScript" EDITFIELD title:"Azure's application name" caption:"Application name:" text:"MyAzureApplication"
	#IF (ecode != 0) {
    #    EXIT
    #}
    #SET applicationURL: (text)

	# Get API token TODO poi ripristinare
	SETL headers: (map())
	#CC "AdoScript" EDITFIELD title:"Azure's API token" caption:"API token (find it at 'Permissions>API tokens'):" text:"API token"
	#IF (ecode != 0) {
    #    EXIT
    #}
	#SET apiToken:(text)
	SET apiToken:("SharedAccessSignature sr=368b8d24-e2f2-4454-844b-93d29bc1dde6&sig=NZqVcdPZH9NFQkuQBvz65txwipMrhJ%2FrUfQBe%2BOk%2FBc%3D&skn=genzo&se=1708081185061")
	SETL headers["Authorization"]: (apiToken)
	#SETL headers["Content-Type"]:("application/json;odata=verbose") todo forse serve solo per le PUT
 

	# GET DEVICE LIST
	SETL url:("https://"+applicationURL+".azureiotcentral.com/api/devices?api-version=2022-07-31")
	HTTP_SEND_REQUEST(url) str_method:("GET") map_reqheaders:(headers) str_reqbody:("") val_respcode:respstat map_respheaders:respheaders str_respbody:str_respbody
	SETL mapRespBody: (fromJson(str_respbody))
	SETL devicesArray:(mapRespBody["value"])

	# GET DEVICE TEMPLATE LIST
	SETL url:("https://"+applicationURL+".azureiotcentral.com/api/deviceTemplates?api-version=2022-07-31")
	HTTP_SEND_REQUEST(url) str_method:("GET") map_reqheaders:(headers) str_reqbody:("") val_respcode:respstat map_respheaders:respheaders str_respbody:str_respbody
	SETL mapRespBody: (fromJson(str_respbody))
	SETL deviceTemplatesArray:(mapRespBody["value"])


	# GET DASHBOARD LIST
	SETL url:("https://"+applicationURL+".azureiotcentral.com/api/dashboards?api-version=2022-10-31-preview")
	HTTP_SEND_REQUEST(url) str_method:("GET") map_reqheaders:(headers) str_reqbody:("") val_respcode:respstat map_respheaders:respheaders str_respbody:str_respbody
	SETL mapRespBody: (fromJson(str_respbody))
	SETL dashboardsArray:(mapRespBody["value"])

	# GET DEVICE GROUP LIST
	SETL url:("https://"+applicationURL+".azureiotcentral.com/api/deviceGroups?api-version=2022-10-31")
	HTTP_SEND_REQUEST(url) str_method:("GET") map_reqheaders:(headers) str_reqbody:("") val_respcode:respstat map_respheaders:respheaders str_respbody:str_respbody
	SETL mapRespBody: (fromJson(str_respbody))
	SETL deviceGroupsArray:(mapRespBody["value"])

	EXTRACT_CLASSIDS

	# For each device in the device list create new objects
	
	SETL objPosX:(2cm)
	SETL objPosY:(2cm)

	# the equivalent of "FOR <device> in <devicesArray>"
	FOR i from:0 to:(LEN devicesArray-1) {
		SET deviceName: (devicesArray[i]["id"])

		CC "Core" CREATE_OBJ modelid:(a_dstmodelid) classid:(deviceClassId)

		SETL deviceId:(objid)
		CC "Core" SET_ATTR_VAL objid:(deviceId) attrname:("name") val:(deviceName)
		CC "Core" SET_ATTR_VAL objid:(deviceId) attrname:("Name") val:(deviceName) # che bastardata...

		CC "Modeling" SET_OBJ_POS objid:(deviceId) x:(objPosX) y:(objPosY)

		# the equivalent of "FOR <deviceTemplate> in <deviceTemplatesArray>"
		FOR j from:0 to:(LEN deviceTemplatesArray-1) {
			SETL capabilityModel: (deviceTemplatesArray[j]["capabilityModel"]) # attribute "displayName" isn't reliable
			SETL deviceTemplateId: (capabilityModel["@id"])
			SETL x:(search(deviceTemplateId, deviceName, 0))		
			# if we found the device template with the same name as the device
			IF ( x != -1 ) {
				SETL deviceTemplateName:(deviceName)	# id is the same as the device id
				SETL capabilities:(capabilityModel["contents"])

				# centrate the "device" and create new line for "attribute" & "Device Operation" objects
				IF(LEN capabilities = 1) {
					CC "Modeling" SET_OBJ_POS objid:(deviceId) x:(objPosX) y:(objPosY)
					SETL objPosY:(objPosY+5cm)
				} ELSE {
					CC "Modeling" SET_OBJ_POS objid:(deviceId) x:(CM(((LEN capabilities-1)/2)*5)) y:(objPosY)
					SETL objPosY:(objPosY+5cm)
				}

				FOR k from:0 to:(LEN capabilities-1) {
					SETL capabilityName:(capabilities[k]["name"])
					SETL type:(capabilities[k]["@type"])
					# if "@type": "Command",
					IF(type(type) = "string") {
						CC "Core" CREATE_OBJ modelid:(a_dstmodelid) classid:(deviceOperationClassId)
						SETL deviceOperationId:(objid)
						CC "Core" SET_ATTR_VAL objid:(deviceOperationId) attrname:("name") val:(capabilityName)
						CC "Core" SET_ATTR_VAL objid:(deviceOperationId) attrname:("Name") val:(capabilityName)
						CC "Modeling" SET_OBJ_POS objid:(deviceOperationId) x:(objPosX) y:(objPosY)
						SETL objPosX:(objPosX+5cm)
            			CC "Core" CREATE_CONNECTOR modelid:(a_dstmodelid) fromobjid:(deviceId) toobjid:(deviceOperationId) classid:(hasOperationClassId)					
					} ELSIF(type(type) = "array") {		# else if "@type": ["Telemetry","<capabilitySemanticType>","NumberValue"],
						SETL capabilitySemanticType:(type[1]) # usually the psm_type is specified in the 2nd element of the "@type" array of the REST GET
						CC "Core" CREATE_OBJ modelid:(a_dstmodelid) classid:(attributeClassId)
						SETL attributeId:(objid)
						CC "Core" SET_ATTR_VAL objid:(attributeId) attrname:("name") val:(capabilityName)
						CC "Core" SET_ATTR_VAL objid:(attributeId) attrname:("Name") val:(capabilityName)
						CC "Core" SET_ATTR_VAL objid:(attributeId) attrname:("psm_type") val:(capabilitySemanticType)
						CC "Modeling" SET_OBJ_POS objid:(attributeId) x:(objPosX) y:(objPosY)
						SETL objPosX:(objPosX+5cm)
						CC "Core" CREATE_CONNECTOR modelid:(a_dstmodelid) fromobjid:(deviceId) toobjid:(attributeId) classid:(hasAttributeClassId)
					}
				}
			}
		}
		SETL objPosX:(2cm)
		SETL objPosY:(objPosY+10cm)
	}

	PRESET_PSM platform:"Azure"

	CC "Modeling" GET_ACT_MODEL
	SETL a_dstmodelid:(modelid)

	# the equivalent of "FOR <dashboard> in <dashboardsArray>"
	FOR i from:0 to:(LEN dashboardsArray-1) 
	{
		SETL tiles:(dashboardsArray[i]["tiles"])
		FOR j from:0 to:(LEN tiles-1) 
		{
			SETL widgetName:(tiles[j]["displayName"])

			SETL configuration:(tiles[j]["configuration"])

			SETL widgetType:(configuration["type"])
			IF(widgetType = "lineChart") {
				SETL widgetType:"Line Chart"
			} ELSIF(widgetType = "lkv") {
				SETL widgetType:"Last Known Value (lkv)"
			}

			SETL widgetsAttachedTo:(configuration["capabilities"])
			
			IF(type(configuration["capabilities"]) = "undefined") {
				NEXT
			}

			# if there are no capabilities linked to the dashboard but only a device create the "widget" object next to the "device" object
			IF(configuration["capabilities"].length = 0) {
				SETL devArr:(configuration["devices"])
				SETL deviceAttachedTo:(devArr[0])

				# get the "device" object coordinate and set new "widget" object position next to it
				CC "Core" GET_OBJ_ID modelid:(a_dstmodelid) classid:(deviceClassId) objname:(deviceAttachedTo)
				SETL devObjId:(objid)
				CC "Core" GET_ATTR_ID classid:(deviceClassId) attrname:"Position"
				CC "Core" GET_ATTR_VAL objid:(devObjId) attrid:(attrid)
        		LEO parse:(val) get-tmm-value:x:"x" get-tmm-value:y:"y"
        		SET x:(x + 5cm)
				#  TODO accomedare beautify "wiget" object position
				#CC "Core" GET_CONNECTORS objid:(devObjId) out
				#IF ( objids.length >= 4) {	 # todo accomedare
				#    CC "AdoScript" INFOBOX(" objids " + STR objids.length)
				#	SET x:(x +  25cm) # add a centimeter each attribute CM(objids.length - 4)
				#}

				CC "Core" CREATE_OBJ modelid:(a_dstmodelid) classid:(widgetClassId) objname:(deviceAttachedTo + " - widget" )
				SETL widget:(objid)
				CC "Core" SET_ATTR_VAL objid:(widget) attrname:("name") val:(widgetName)
				CC "Core" SET_ATTR_VAL objid:(widget) attrname:("Name") val:(widgetName)
				CC "Core" SET_ATTR_VAL objid:(widget) attrname:("psm_type") val:(widgetType)
        		CC "Modeling" SET_OBJ_POS objid:(widget) x:(x) y:(y)
				SET x:(x+5cm)
				CC "Core" CREATE_CONNECTOR modelid:(a_dstmodelid) fromobjid:(devObjId) toobjid:(widget) classid:(devHasWidgetClassId)				
			} ELSE {
				# else, link the new "widget" objects underneath the "attribute" objects
				SETL capabilitiesArray:(configuration["capabilities"])
				FOR k from:0 to:(LEN capabilitiesArray-1) {

					SETL capability:(capabilitiesArray[k])
					SETL attributeObjectName:(capability["capability"])

					CC "Core" GET_OBJ_ID modelid:(a_dstmodelid) classid:(attributeClassId) objname:(attributeObjectName)
					SETL attrObjId:(objid)
					CC "Core" GET_ATTR_ID classid:(attributeClassId) attrname:"Position"
					CC "Core" GET_ATTR_VAL objid:(attrObjId) attrid:(attrid)
        			LEO parse:(val) get-tmm-value:x:"x" get-tmm-value:y:"y"
        			SET y:(y + 5cm)
					CC "Core" CREATE_OBJ modelid:(a_dstmodelid) classid:(widgetClassId) objname:(widgetName)
					SETL widget:(objid)
					CC "Core" SET_ATTR_VAL objid:(widget) attrname:("name") val:(attributeObjectName + " - "+ widgetType)
					CC "Core" SET_ATTR_VAL objid:(widget) attrname:("Name") val:(attributeObjectName + " - "+ widgetType)
					CC "Core" SET_ATTR_VAL objid:(widget) attrname:("psm_type") val:(widgetType)
        			CC "Modeling" SET_OBJ_POS objid:(widget) x:(x) y:(y)
					CC "Core" CREATE_CONNECTOR modelid:(a_dstmodelid) fromobjid:(attrObjId) toobjid:(widget) classid:(hasWidgetClassId)
				}
			}
		}
	}

}

# Call this procedure once to avoid repeating n times the GET_CLASS_ID command call
PROCEDURE global EXTRACT_CLASSIDS 
{
	# Classes
	CC "Core" GET_CLASS_ID classname:("device")
	SETG deviceClassId:(classid)
	CC "Core" GET_CLASS_ID classname:("attribute")
	SETG attributeClassId:(classid)
	CC "Core" GET_CLASS_ID classname:("Device Operation")
	SETG deviceOperationClassId:(classid)
	CC "Core" GET_CLASS_ID classname:("widget")
	SETG widgetClassId:(classid)

	# Relation classes
	CC "Core" GET_CLASS_ID classname:("hasAttribute")
	SETG hasAttributeClassId:(classid)
	CC "Core" GET_CLASS_ID classname:("hasOperation")
	SETG hasOperationClassId:(classid)
	CC "Core" GET_CLASS_ID classname:("hasWidget")
	SETG hasWidgetClassId:(classid)
	CC "Core" GET_CLASS_ID classname:("devHasWidget")
	SETG devHasWidgetClassId:(classid)
}
